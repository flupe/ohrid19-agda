-- WHILE language (C compatible syntax)

-- A BNFC file consists of comments, pragmas, and labelled Backus-Naur Form
-- (LBNF) rules
--
--   RuleName. NonTerminal ::= SententialForm ;
--
-- where RuleName and NonTerminal stand for identifiers
-- and SententialForm for a space-separated sequence
-- of terminals and non-terminals.

-- The first NonTerminal defines the language generated by the grammar.

Prg.    Program ::= "int" "main" "(" ")" "{" "printInt" Exp4 ";" "}";

EInt.   Exp3 ::= Integer;
EBool.  Exp3 ::= Boolean;

EPlus.  Exp2 ::= Exp2 "+" Exp3;  -- left-associative
EGt.    Exp1 ::= Exp2 ">" Exp2;  -- non-associative
EAnd.   Exp  ::= Exp "&&" Exp1;  -- left-associative

coercions Exp 4;

-- Added by the coercions pragma:
--
-- _.   Exp4 ::= "(" Exp ")";
-- _.   Exp3 ::= Exp4       ;
-- _.   Exp2 ::= Exp3       ;
-- _.   Exp1 ::= Exp2       ;
-- _.   Exp  ::= Exp1       ;

BTrue.  Boolean ::= "true" ;
BFalse. Boolean ::= "false";

TBool.  Type ::= "bool";
TInt.   Type ::= "int" ;

-- Pragmas for the lexer:

comment "#"      ;  -- line comment starting with #
comment "//"     ;  -- line comment starting with //
comment "/*" "*/";  -- block comment between /* and */
