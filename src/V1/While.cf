-- WHILE language (C compatible syntax)

-- A BNFC file consists of comments, pragmas, and labelled Backus-Naur Form
-- (LBNF) rules
--
--   RuleName. NonTerminal ::= SententialForm ;
--
-- where RuleName and NonTerminal stand for identifiers
-- and SententialForm for a space-separated sequence
-- of terminals and non-terminals.

-- The first NonTerminal defines the language generated by the grammar.

Prg.    Program ::= "int" "main" "(" ")" "{" "printInt" Exp5 ";" "}";

EInt.   Exp5 ::= Integer;
EBool.  Exp5 ::= Boolean;

EMul.  Exp4 ::= Exp4 "*" Exp5;            -- left-associative
EDiv.  Exp4 ::= Exp4 "/" Exp5;            -- left-associative

EPlus.  Exp3 ::= Exp3 "+" Exp4;           -- left-associative
EMinus. Exp3 ::= Exp3 "-" Exp4;           -- left-associative
EGt.    Exp2 ::= Exp3 ">" Exp3;           -- non-associative
EAnd.   Exp1 ::= Exp1 "&&" Exp2;          -- left-associative
ECond.  Exp  ::= Exp1 "?" Exp1 ":" Exp1;  -- non-associative

coercions Exp 6;

-- Added by the coercions pragma:
--
-- _.   Exp6 ::= "(" Exp ")";
-- _.   Exp5 ::= Exp6       ;
-- _.   Exp4 ::= Exp5       ;
-- _.   Exp3 ::= Exp4       ;
-- _.   Exp2 ::= Exp3       ;
-- _.   Exp1 ::= Exp2       ;
-- _.   Exp  ::= Exp1       ;

BTrue.  Boolean ::= "true" ;
BFalse. Boolean ::= "false";

TBool.  Type ::= "bool";
TInt.   Type ::= "int" ;

-- Pragmas for the lexer:

comment "#"      ;  -- line comment starting with #
comment "//"     ;  -- line comment starting with //
comment "/*" "*/";  -- block comment between /* and */
